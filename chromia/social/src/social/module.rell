module;
import lib.ft4.core.accounts.{ account, single_sig_auth_descriptor, create_account_with_auth };
import lib.ft4.auth;


@extend(auth.auth_handler)
function () = auth.add_auth_handler(
  flags = ["MySession"]
);


entity user {
  key name;
  key id: byte_array;
  key account;
}

entity page {
  key ticker: name;
  key name;
  key id: byte_array;
  key account;
}

entity follower {
  index page;
  index follower: user;
  key page, follower;
}

entity post {
  timestamp = op_context.last_block_time;
  index page;
  content: text;
}

operation create_user(name, pubkey) {
    val account = create_account_with_auth(single_sig_auth_descriptor(pubkey, set(["A", "T", "MySession"])));
    create user(name, id = pubkey, account = account);
}

operation create_page(ticker:name, name, pubkey) {
    val account = create_account_with_auth(single_sig_auth_descriptor(pubkey, set(["A", "T", "MySession"])));
    create page(ticker = ticker, name, id = pubkey, account = account);
}

operation make_post(content: text) {
    val account = auth.authenticate();
    val page = page @ { account };
    //require_is_signer(page_id);
    require(content.size() <= 255, "A post cannot be longer than 255 letters");
    //require_page(page_id);
    create post(
        page, content
    );
}

operation follow_page(page_id: byte_array) {
    val account = auth.authenticate();
    //require_is_signer(user_id);
    val user = user @ { account };
    val page = require_page(page_id);
    create follower(
        page = page,
        follower = user
    );
}

operation unfollow_page(page_id: byte_array) {
    val account = auth.authenticate();
    //require_is_signer(user_id);
    val user = user @ { account };
    val unfollow = require_page(page_id);
    delete follower @? { .page == unfollow, .follower == user };
}

query get_user_name(user_id: byte_array): text {
  return format_user(require_user(user_id));
}

query get_followers_count(page_id: pubkey): integer {
  return follower @ { .page == require_page(page_id) } ( @sum 1 );
}

query get_following_count(user_id: byte_array): integer {
  return follower @ { .follower == require_user(user_id) } (@sum 1);
}

query is_following(my_id: byte_array, page_id: byte_array) =
  exists(follower @? { .page.id == page_id, .follower.id == my_id });


struct post_dto {
  timestamp;
  user: struct<page>;
  content: text;
}


query get_posts(
  user_id: byte_array,
  pointer: integer,
  n_posts: integer
): (pointer: integer, posts: list<post_dto>) {
  val posts = (user, follower, post) @* {
    user.id == user_id,
    follower.follower == user,
    post.page == follower.page
  } (
    @sort_desc @omit post.rowid,
    post_dto(
        post.timestamp,
        post.content,
        user = post.page.to_struct()
    )
  ) offset pointer limit n_posts;

  return (
    pointer = pointer + posts.size(),
    posts = posts
  );
}


query get_users(pointer: integer, n_users: integer) {
  val users = user @* {} (name = format_user($), id = .id) offset pointer limit n_users;
  return (
    pointer = pointer + users.size(),
    users = users
  );
}


function require_is_signer(pubkey) = require(op_context.is_signer(pubkey), "User must sign this operation");
function require_user(id: byte_array) = require(user @? { id }, "User with id %b does not exist".format(id));
function require_page(id: byte_array) = require(page @? { id }, "Page with id %b does not exist".format(id));
function format_user(user) = "%s#%s".format(user.name, user.id.to_hex().sub(0, 5));